<start> = assignment | expr
expr = add-sub
<add-sub> = pow-term | mul-div | add | sub |  variable
add = add-sub <'+'> mul-div
sub = add-sub <'-'> mul-div
<mul-div> = pow-term | mul | div
mul = mul-div <'*'> pow-term
div = mul-div <'/'> pow-term
<pow-term> = pow | term
pow = pow-term <'^'> term
<trig> = sin | cos | tan | acos | asin | atan
log = <#'\s*'> <'log('> expr <')'> <#'\s*'>
ln = <#'\s*'> <'ln('> expr <')'> <#'\s*'>
sin = <#'\s*'> <'sin('> expr <')'> <#'\s*'>
cos = <#'\s*'> <'cos('> expr <')'> <#'\s*'>
tan = <#'\s*'> <'tan('> expr <')'> <#'\s*'>
atan = <#'\s*'> <'atan('> expr <')'> <#'\s*'>
acos = <#'\s*'> <'acos('> expr <')'> <#'\s*'>
asin = <#'\s*'> <'asin('> expr <')'> <#'\s*'>
<posterm> = log | ln | trig | percent | scientific | number | variable | <#'\s*'> <'('> expr <')'> <#'\s*'>
negterm = <#'\s*'> <'-'> posterm
<term> = negterm | posterm
scientific = #'\s*[0-9]+\.?[0-9]*(e|E)-?[0-9]+()\s*'
number = #'\s*\d+(,\d+)*(\.\d*)?\s*'
percent = number <'%'> <#'\s*'>
variable = #'\s*[a-zA-Z]+(\_+[a-zA-Z]+)*\s*'
toassign = #'\s*[a-zA-Z]+(\_+[a-zA-Z]+)*\s*'
assignment = toassign <#'\s*'> <'='> <#'\s*'> expr